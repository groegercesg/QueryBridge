?start:   
         | or_op

?or_op:
         | or_op "OR" and_op -> or
         | and_op

?and_op:
         | and_op "AND" null_op -> and
         | null_op

?null_op: 
         | eq_op "IS NOT NULL" -> isnotnull
         | eq_op

?eq_op: 
         | sort_op "<=" eq_op -> leq
         | sort_op ">=" eq_op -> geq
         | sort_op "<" eq_op -> lt
         | sort_op ">" eq_op -> gt
         | sort_op "=" eq_op -> eq
         | sort_op "!=" eq_op -> neq
         | sort_op

?sort_op:
         | aggr_op "DESC" -> sort_desc
         | aggr_op "ASC" -> sort_asc
         | aggr_op

?aggr_op: 
         | "count(*)" -> count_star
         | sum
         | avg
         | min
         | max
         | countdistinct
         | count
         | suffix
         | prefix
         | sum_op

?sum_op:  
         | product_op
         | sum_op "+" product_op -> add
         | sum_op "-" product_op -> sub

?product_op:
         | element
         | product_op "*" element -> mul
         | product_op "/" element -> div

?element:
         | NUMBER -> number
         | "-" element -> neg
         | date
         | col_ref
         | "(" or_op ")"
         | string

STRING: /[a-zA-Z0-9_.-]{2,}/

?string: "'" STRING* "'" -> string

// Parsing date
YEAR  : DIGIT DIGIT DIGIT DIGIT
MONTH : "0" "1".."9"
      | "1" "1".."2" 
DAY   : "0" DIGIT
      | "1" DIGIT 
      | "2" DIGIT
      | "3" "0".."1" 

?date: YEAR "-"? MONTH "-"? DAY -> date

// Recognising Column References
?col_ref:
         | table "." column -> table_ref
         | column 

?underscore: "_"
?column: WORD underscore WORD -> col_ref
?table: WORD

// Aggregation operations
?sum: "sum(" or_op ")" -> sum
?avg: "avg(" or_op ")" -> avg
?min: "min(" or_op ")" -> min
?max: "max(" or_op ")" -> max
?count: "count(" or_op ")" -> count
?countdistinct: "count(DISTINCT" or_op ")" -> countdistinct
?suffix: "suffix(" col_ref "," string ")" -> suffix
?prefix: "prefix(" col_ref "," string ")" -> prefix

// imports stuff from library
%import common.WORD  
%import common.LETTER   
%import common.NUMBER
%import common.DIGIT

%import common.WS_INLINE
%ignore WS_INLINE